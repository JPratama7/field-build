pkgname=mesa-all
pkgdesc="Mesa open-source implementation of the OpenGL specification"
pkgver=21.3.7
_pkgver=${pkgver//_/-}
pkgrel=1
arch=('x86_64')
makedepends=('git' 'python-mako' 'xorgproto' 'llvm-all'
  'libxml2' 'libx11' 'libvdpau' 'libva' 'elfutils' 'libxrandr'
  'ocl-icd' 'wayland-protocols' 'meson' 'ninja' 'glslang' 'valgrind')
depends=('libdrm' 'libxxf86vm' 'libxdamage' 'libxshmfence' 'libelf'
  'libomxil-bellagio' 'libglvnd' 'wayland' 'lm_sensors' 'libclc' 'vulkan-icd-loader' 'zstd' 'expat')
optdepends=('opengl-man-pages: for the OpenGL API man pages')
provides=('mesa' "mesa=${pkgver}" 'opencl-mesa' 'vulkan-intel' 'vulkan-radeon' 'vulkan-mesa-layer' 'libva-mesa-driver' 'mesa-vdpau' 'vulkan-swrast' 'vulkan-driver' 'opengl-driver' 'opencl-driver' 'mesa-libgl')
conflicts=('mesa' 'opencl-mesa' 'vulkan-intel' 'vulkan-radeon' 'vulkan-mesa-layer' 'libva-mesa-driver' 'mesa-vdpau' 'vulkan-swrast')
url="https://www.mesa3d.org"
license=('custom')
source=('https://mesa.freedesktop.org/archive/mesa-${pkgver}.tar.xz{,.sig}'
        'LICENSE')
sha512sums=('0991543e9435457fa4d077517408b3f197be32ed61a6c7ca34ddb3906eed208791f1a57227f74115f99df18e612efab1d2c6809b7cf426d273633b53d4aefc88'
            'SKIP'
            'f9f0d0ccf166fe6cb684478b6f1e1ab1f2850431c06aa041738563eb1808a004e52cdec823c103c9e180f03ffc083e95974d291353f0220fe52ae6d4897fecc7')
validpgpkeys=('8703B6700E7EE06D7A39B8D6EDAE37B02CEB490D'  # Emil Velikov <emil.l.velikov@gmail.com>
              '946D09B5E4C9845E63075FF1D961C596A7203456'  # Andres Gomez <tanty@igalia.com>
              'E3E8F480C52ADD73B278EE78E1ECBE07D7D70895'  # Juan Antonio Su√°rez Romero (Igalia, S.L.) <jasuarez@igalia.com>
              'A5CC9FEC93F2F837CB044912336909B6B25FADFA'  # Juan A. Suarez Romero <jasuarez@igalia.com>
              '71C4B75620BC75708B4BDB254C95FAAB3EB073EC'  # Dylan Baker <dylan@pnwbakers.com>
              '57551DE15B968F6341C248F68D8E31AFC32428A6') # Eric Engestrom <eric@engestrom.ch>


DRI_DRIVERS="i915,i965,r100,r200,nouveau"
GALLIUM_DRIVERS="r300,r600,radeonsi,nouveau,virgl,svga,swrast,iris,crocus,zink,d3d12"
VULKAN_DRIVERS="amd,intel,swrast"
VULKAN_LAYERS="device-select,intel-nullhw,overlay"

_extra_lto_flag="-D b_lto_mode=full"

_patch() {

  echo -e "\E[1;33m Apply patch: ${1}\E[0m"
  patch -p1 -i "${srcdir:?}/${1}"

}

prepare() {

  cd "${srcdir:?}/mesa-${_pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir:?}/mesa-${pkgver} directory! Prepare Failed! \E[0m"
    exit 1
  )

}

build() {

  meson setup mesa-${_pkgver} build \
    --prefix /usr \
    --libexecdir lib \
    --sbindir bin \
    --auto-features enabled \
    --wrap-mode nodownload \
    -D debug=false \
    -D optimization=2 \
    -D b_ndebug=true \
    -D b_lto=true ${_extra_lto_flag} \
    -D b_pie=true \
    -D sysconfdir=/etc \
    -D platforms=x11,wayland \
    -D osmesa=true \
    -D dri-drivers="${DRI_DRIVERS}" \
    -D gallium-drivers="${GALLIUM_DRIVERS}" \
    -D vulkan-drivers="${VULKAN_DRIVERS}" \
    -D vulkan-layers="${VULKAN_LAYERS}" \
    -D gallium-extra-hud=true \
    -D gallium-omx=bellagio \
    -D gallium-nine=true \
    -D gallium-opencl=icd \
    -D gallium-va=enabled \
    -D gallium-vdpau=enabled \
    -D gallium-xa=enabled \
    -D gallium-xvmc=disabled \
    -D llvm=enabled \
    -D dri3=enabled \
    -D egl=enabled \
    -D gbm=enabled \
    -D gles1=disabled \
    -D gles2=enabled \
    -D glvnd=true \
    -D glx=dri \
    -D lmsensors=enabled \
    -D shared-glapi=enabled \
    -D zstd=enabled \
    -D microsoft-clc=disabled \
    -D libunwind=disabled \
    -D valgrind=disabled

  meson compile -C build

}

package() {

  DESTDIR="${pkgdir:?}" meson install -C build

  ln -s /usr/lib/libGLX_mesa.so.0 "${pkgdir:?}/usr/lib/libGLX_indirect.so.0"

  install -Dm644 "${srcdir:?}/LICENSE" "${pkgdir:?}/usr/share/licenses/${pkgbase:?}/LICENSE"

}
