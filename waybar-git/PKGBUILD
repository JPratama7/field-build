# Maintainer: Jose Chasey Pratama <josepratama080@gmail.com>
# Contributor: Alexis Rouillard <contact@arouillard.fr>

pkgname=waybar-git
pkgver=r2429.09142fa3
pkgrel=1
pkgdesc='Highly customizable Wayland bar for Sway and Wlroots based compositors (GIT)'
arch=('x86_64')
url='https://github.com/Alexays/Waybar/'
license=('MIT')
provides=('waybar')
conflicts=('waybar')
depends=(
    'atkmm'
    'cairomm'
    'fmt'
    'gcc-libs'
    'glib2'
    'glibc'
    'glibmm'
    'gtk-layer-shell'
    'gtk3'
    'gtkmm3'
    'jack'
    'jsoncpp'
    'libdbusmenu-gtk3'
    'libevdev'
    'libinput'
    'libmpdclient'
    'libnl'
    'libpipewire'
    'libpulse'
    'libsigc++'
    'libwireplumber'
    'libxkbcommon'
    'playerctl'
    'sndio'
    'spdlog'
    'systemd-libs'
    'upower'
    'wayland'
)
makedepends=(
    'git'
    'catch2'
    'cmake'
    'meson'
    'python-packaging' # gdbus-codegen
    'scdoc' # For generating manpages
    'wayland-protocols'
)
backup=(
    etc/xdg/waybar/config
    etc/xdg/waybar/style.css
)
optdepends=(
    'otf-font-awesome: Icons in the default configuration'
)

source=("${pkgname}::git+https://github.com/Alexays/Waybar")

sha1sums=('SKIP')

pkgver() {
    cd "${srcdir}/${pkgname}"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd "${srcdir}/${pkgname}"
}

build() {
    cd "${srcdir}/${pkgname}"
    rm -rf "${srcdir}/build"
    
    arch-meson --prefix=/usr \
          --buildtype=plain \
          --auto-features=enabled \
          -Dcpp_std=c++20 \
          -Dexperimental=true \
          -Dcava=disabled \
          -Dtests=enabled \
          "${srcdir}/build"

    ninja -C "${srcdir}/build"
}

check() {
    cd "${srcdir}/build"
    meson test --print-errorlogs --no-rebuild --suite waybar
}

package() {
    DESTDIR="$pkgdir" ninja -C "${srcdir}/build" install
    install -Dm644 "${srcdir}/${pkgname}/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname/"
}