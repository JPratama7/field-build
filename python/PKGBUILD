

# Maintainer: Tobias Kunze <r@rixx.de>
# Maintained at https://github.com/rixx/pkgbuilds, feel free to submit patches

pkgname=python38
pkgver=3.8.14
pkgrel=2
_pybasever=3.8
_pymajver=3
pkgdesc="Major release 3.8 of the Python high-level programming language"
arch=('i686' 'x86_64')
license=('custom')
url="https://www.python.org/"
depends=('expat' 'bzip2' 'gdbm' 'openssl' 'libffi' 'zlib')
makedepends=('tk' 'sqlite' 'bluez-libs' 'mpdecimal')
optdepends=('tk: for tkinter' 'sqlite')
source=(
    https://www.python.org/ftp/python/${pkgver}/Python-${pkgver}.tar.xz
    https://www.python.org/ftp/python/${pkgver}/Python-${pkgver}.tar.xz.asc
    https://raw.githubusercontent.com/JPratama7/test-cicd/master/python/mpdecimal-2.5.1.patch
)
sha256sums=('6f309077012040aa39fe8f0c61db8c0fa1c45136763299d375c9e5756f09cf57'
            'SKIP'
            'SKIP')
            
validpgpkeys=('E3FF2839C048B25C084DEBE9B26995E310250568')
provides=("python=$pkgver")
options=(!lto)

prepare() {
  cd "${srcdir}/Python-${pkgver}"

  patch -p1 -i "${srcdir}/mpdecimal-2.5.1.patch"

  # FS#23997
  sed -i -e "s|^#.* /usr/local/bin/python|#!/usr/bin/python|" Lib/cgi.py

  # Ensure that we are using the system copy of various libraries (expat, zlib and libffi),
  # rather than copies shipped in the tarball
  rm -rf Modules/expat
  rm -rf Modules/zlib
  rm -rf Modules/_ctypes/{darwin,libffi}*
  rm -rf Modules/_decimal/libmpdec
}

build() {
  cd "${srcdir}/Python-${pkgver}"


  unset CFLAGS
  unset CXXFLAGS
  unset LDFLAGS

  EXTRA_CFLAGS="${_llvm} ${_polly} -DOPENSSL_NO_SSL2"
  ./configure --prefix=/usr \
              --enable-shared \
              --with-threads \
              --with-lto \
              --enable-optimizations \
              --with-computed-gotos \
              --enable-ipv6 \
              --with-system-expat \
              --with-dbmliborder=gdbm:ndbm \
              --with-system-libmpdec \
              --enable-loadable-sqlite-extensions \
              --without-ensurepip

  LC_CTYPE=en_US.UTF-8 make regen-all EXTRA_CFLAGS="${EXTRA_CFLAGS}"
  LC_CTYPE=en_US.UTF-8 make EXTRA_CFLAGS="${EXTRA_CFLAGS}"
}

check() {

  # test_socket: test.test_socket.RDSTest.testPeek hangs https://bugs.python.org/issue35247
  # test_tk: tkinter.test.test_tkinter.test_colorchooser.DefaultRootTest hangs

  cd "${srcdir:?}/Python-${pkgver}" || (
    echo -e "\E[1;31mCan't cd to ${srcdir}/Python-${pkgver} directory! Check Failed! \E[0m"
    exit 1
  )

  LD_LIBRARY_PATH="${srcdir:?}/Python-${pkgver}":${LD_LIBRARY_PATH} \
    LC_CTYPE=en_US.UTF-8 \
    "${srcdir:?}/Python-${pkgver}/python" -m test.regrtest -v -uall -x test_socket -x test_tk -x test_zlib

}

package() {
  cd "${srcdir}/Python-${pkgver}"
  # altinstall: /usr/bin/pythonX.Y but not /usr/bin/python or /usr/bin/pythonX
  make DESTDIR="${pkgdir}" altinstall maninstall

  # Avoid conflicts with the main 'python' package.
  rm -f "${pkgdir}/usr/lib/libpython${_pymajver}.so"
  rm -f "${pkgdir}/usr/share/man/man1/python${_pymajver}.1"

  # Clean-up reference to build directory
  sed -i "s|$srcdir/Python-${pkgver}:||" "$pkgdir/usr/lib/python${_pybasever}/config-${_pybasever}-${CARCH}-linux-gnu/Makefile"

  # Add useful scripts FS#46146
  install -dm755 "${pkgdir}"/usr/lib/python${_pybasever}/Tools/{i18n,scripts}
  install -m755 Tools/i18n/{msgfmt,pygettext}.py "${pkgdir}"/usr/lib/python${_pybasever}/Tools/i18n/
  install -m755 Tools/scripts/{README,*py} "${pkgdir}"/usr/lib/python${_pybasever}/Tools/scripts/

  # License
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}