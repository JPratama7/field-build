
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Ren√© Wagner < rwagner at rw-net dot de >
# Contributor: Diab Neiroukh <lazerl0rd@thezest.dev>

pkgname=snmalloc
pkgver=0.6.2
pkgrel=1
pkgdesc='General-purpose allocator with excellent performance characteristics'
arch=('x86_64')
url='https://github.com/microsoft/snmalloc'
license=('MIT')
depends=('glibc')
makedepends=('git' 'cmake')
options=('!lto' 'staticlibs')
_commit='dc1268886a5d49d38a54e5d1402b5924a71fee0b'
source=("$pkgname::git+$url#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

build() {
  cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -B build \
    -S "$pkgname"

  cmake --build build
}

check() {
  cd build
  ls -al
  ctest --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$pkgname/LICENSE"
}