pkgbase=thorium-browser
pkgname=${pkgbase}-bin
pkgver=111.0.5563.111
pkgrel=1
pkgdesc="Chromium fork focused on high performance and security."
arch=('x86_64')
url="https://github.com/Alex313031/Thorium"
license=('GPLv3')
depends=('gtk3' 'nss' 'alsa-lib' 'xdg-utils' 'libxss' 'libcups' 'libgcrypt'
         'ttf-liberation' 'libxtst' 'dbus' 'libpulse' 'pciutils' 'libva'
         'desktop-file-utils' 'hicolor-icon-theme')
optdepends=(
	"pipewire: WebRTC desktop sharing under Wayland"
	"kdialog: for file dialogs in KDE"
	"gnome-keyring: for storing passwords in GNOME keyring"
    "org.freedesktop.secrets: password storage backend on GNOME / Xfce"
    "kwallet: support for storing passwords in KWallet on Plasma"
)

install=$pkgbase.install
source=("https://github.com/Alex313031/Thorium/releases/download/M${pkgver}/${pkgbase}_${pkgver}-1_amd64.deb"
	"$pkgbase.sh")
sha512sums=('b91554cc990241ed632c4a4abb14181b1598018716794ccf251d6baf77bca691722558cdcdbc5f88087ceb356a6cdfbf9095038189ecb7fa0777975a1ae13b4f'
            '91a532fde9e6e87d65b909a170d32808f1c05367104cdaab07043fef0de66bdfc307a4138584cd1eedc169a03207372df39c09268ab1ec741432e79e0dcc2dfb')

package() {
	bsdtar -xf data.tar.xz -C "$pkgdir/"
	install -m755 $pkgbase.sh "$pkgdir"/usr/bin/$pkgbase
	chmod 4755 "$pkgdir/opt/chromium.org/thorium/chrome-sandbox"
	
	# Icons
	for i in 16x16 24x24 32x32 48x48 64x64 128x128 256x256; do
		install -Dm644 "$pkgdir"/opt/chromium.org/thorium/product_logo_${i/x*/}.png "$pkgdir"/usr/share/icons/hicolor/$i/apps/thorium-browser.png
	done

	install -Dm644 "$pkgdir/opt/chromium.org/thorium/thorium_shell.png" -t "$pkgdir/usr/share/icons/hicolor/256x256/apps/"

	rm -r \
		"$pkgdir"/etc/cron.daily/ \
		"$pkgdir"/opt/chromium.org/thorium/cron/ \
		"$pkgdir"/opt/chromium.org/thorium/product_logo_*.{png,xpm} \
		"$pkgdir"/usr/share/menu/
}